[var n 0]
[var pi 3.14159265358]
[var solarMass [ * 4 pi pi]]
[var daysPerYear 365.24]
[var N 5]

[var sysV [list
    [list
        0.0 0.0 0.0 solarMass
    ]
    [list
        [* 1.66007664274403694e-03 daysPerYear]
        [* 7.69901118419740425e-03 daysPerYear]
        [* -6.90460016972063023e-05 daysPerYear]
        [* 9.54791938424326609e-04 solarMass]
    ]
    [list
        [* -2.76742510726862411e-03 daysPerYear]
        [* 4.99852801234917238e-03 daysPerYear]
        [* 2.30417297573763929e-05 daysPerYear]
        [* 2.85885980666130812e-04 solarMass]
    ]
    [list
        [* 2.96460137564761618e-03 daysPerYear]
        [* 2.37847173959480950e-03 daysPerYear]
        [* -2.96589568540237556e-05 daysPerYear]
        [* 4.36624404335156298e-05 solarMass]
    ]
    [list
        [* 2.68067772490389322e-03 daysPerYear]
        [* 1.62824170038242295e-03 daysPerYear]
        [* -9.51592254519715870e-05 daysPerYear]
        [* 5.15138902046611451e-05 solarMass]
    ]
]]

[var sysS [list
    [list 0.0 0.0 0.0]
    [list 4.84143144246472090e+00 -1.16032004402742839e+00 -1.03622044471123109e-01]
    [list 8.34336671824457987e+00 4.12479856412430479e+00 -4.03523417114321381e-01]
    [list 1.28943695621391310e+01 -1.51111514016986312e+01 -2.23307578892655734e-01]
    [list 1.53796971148509165e+01 -2.59193146099879641e+01 1.79258772950371181e-01]
]]

[var offsetMomentum [lambda []
    [var px [var py [var pz 0]]]
    [var i 0]
    [while [< i N]
        [var m [load sysV i 3]]
        [inc px [* [sysV:i:0] m]]
        [inc py [* [sysV:i:1] m]]
        [inc pz [* [sysV:i:2] m]]
        [inc i 1]
    ]

    [set [sysV:0:0] [/ [- px] solarMass]]
    [set [sysV:0:1] [/ [- py] solarMass]]
    [set [sysV:0:2] [/ [- pz] solarMass]]
]]

[var energy [lambda []
    [var e 0]
    [var i 0]
    [while [< i N]
        [var x [load sysV i 0]]
        [var y [load sysV i 1]]
        [var z [load sysV i 2]]

        [inc e [* 0.5 [load sysV i 3] [+ [* x x] [* y y] [* z z]]]]

        [var j [+ i 1]]
        [while [< j N]
            [var dx [- [load sysS i 0] [load sysS j 0]]]
            [var dy [- [load sysS i 1] [load sysS j 1]]]
            [var dz [- [load sysS i 2] [load sysS j 2]]]

            [var distance  [math/sqrt [+ [* dx dx] [* dy dy] [* dz dz]]]]
            [set e [- e [/ [* [load sysV i 3] [load sysV j 3]] distance]]]
            [inc j 1]
        ]
        [inc i 1]
    ]

    [ret e]
]]

[var advance [lambda [dt]
    [var i 0]
    [while [< i [- N 1]]
        [var sysVi [load sysV i]]
        [var sysSi [load sysS i]]

        [var _vx [load sysVi 0]]
        [var _vy [load sysVi 1]]
        [var _vz [load sysVi 2]]
        [var j [+ i 1]]

        [while [< j N]
            [var sysSj [load sysS j]]
            [var sysVj [load sysV j]]

            [var dx [- [load sysSi 0] [load sysSj 0]]]
            [var dy [- [load sysSi 1] [load sysSj 1]]]
            [var dz [- [load sysSi 2] [load sysSj 2]]]

            [var dSquared [+ [* dx dx] [* dy dy] [* dz dz]]] 
            [var distance [math/sqrt dSquared]]
            [var mag [/ dt [* dSquared distance]]]

            [var mi [* [load sysVi 3] mag]]
            [var m [* [load sysVj 3] mag -1]]

            [inc _vx [* dx m]]
            [inc _vy [* dy m]]
            [inc _vz [* dz m]]

            [inc [sysVj:0] [* dx mi]]
            [inc [sysVj:1] [* dy mi]]
            [inc [sysVj:2] [* dz mi]]
            [inc j 1]
        ]

        [store sysVi 0 _vx]
        [store sysVi 1 _vy]
        [store sysVi 2 _vz]
        [inc i 1]
    ]

    [set i 0]
    [while [< i N]
        [var sysSi [load sysS i]]
        [var sysVi [load sysV i]]
        [inc [sysSi:0] [* dt [load sysVi 0]]]
        [inc [sysSi:1] [* dt [load sysVi 1]]]
        [inc [sysSi:2] [* dt [load sysVi 2]]]
        [inc i 1]
    ]
]]

[offsetMomentum]
#[assert [energy] -0.16907516382527074 ]
[stdout/println [energy]]
[var i 0]
[while [< i 5000000]
    [advance 0.01]
    [inc i 1]
]
[stdout/println [energy]]
#[assert  [energy] -0.16907807065622285 ]